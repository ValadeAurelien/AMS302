####################
### pre commands ###
####################
set term epslatex
inputdir='./datas/'
outputdir='./Graphics/'

set xlabel "$x$"
set ylabel "$\\phi(x, \mu)$"


######################
### loop_nb_pts_mu ###
######################
set output outputdir."loop_nb_pts_mu_delta.tex"
plot for [i=2:16:2] inputdir.'loop_nb_pts_mu/output_2_'.i u 1:2 w l t '$N_{\mu}$='.i

set key center bottom
set output outputdir."loop_nb_pts_mu_cste.tex"
plot for [i=2:16:2] inputdir.'loop_nb_pts_mu/output_1_'.i u 1:2 w l t '$N_{\mu}$='.i
set key default


##############
### Basics ###
##############
set key bottom
set output outputdir."output_1_neg1_5_1.tex"
plot inputdir."output_1_-1_5_1" u 1:3 w l lw 1 lc 6  t 'théorique', '' u 1:2 w l lw 1 lc 7  t 'approximation'

set output outputdir."output_1_1_5_1.tex"
plot inputdir."output_1_1_5_1" u 1:3 w l lw 1 lc 6  t 'théorique', '' u 1:2 w l lw 1 lc 7  t 'approximation'
set key default

set output outputdir."output_1_05_5_2.tex"
plot inputdir."output_1_.5_5_2" u 1:3 w l lw 1 lc 6  t 'théorique', '' u 1:2 w l lw 1 lc 7  t 'approximation'


#################
### Two steps ###
#################
set output outputdir."output_two_steps_1_1_1_2.tex"
plot inputdir."output_two_steps_1_1_1_2" u 1:3 w l lw 1 lc 6  t 'théorique', '' u 1:2 w l lw 1 lc 7  t 'approximation'

set output outputdir."output_two_steps_1_05_1_2.tex"
plot inputdir."output_two_steps_1_.5_1_2" u 1:3 w l lw 1 lc 6  t 'théorique', '' u 1:2 w l lw 1 lc 7  t 'approximation'

set output outputdir."output_two_steps_1_05_3_2.tex"
plot inputdir."output_two_steps_1_.5_3_2" u 1:2 w l lw 1 lc 7  t 'approximation'


#######################
### Limit sigma_a=0 ###
#######################

set output outputdir.'output_limit_sigma_a_zero.tex'
plot inputdir.'output_limit_sigma_a_zero' u 1:2 w l lw 1 lc 7 t 'approximation' , 3./2*x*(1-x) lc 6 t 'théorie'


####################
### loop_nb_segs ###
####################

f(x) = a*x+b
g(x) = c*x+d
a=-1
c=-1
b=.2
d=.2
fit f(x) inputdir."loop_nb_segs/output_2_all" u (log($1)):(log($2)) via a,b
fit g(x) inputdir."loop_nb_segs/output_1_all" u (log($1)):(log($2)) via c,d
fexp(x) = b*exp(a*x)
gexp(x) = d*exp(c*x)
d=b
set xlabel '$N_x$'
set ylabel 'Erreur relative $L^2$'
set logscale

set output outputdir."nb_segs_diff_L2_delta.tex"
plot inputdir."loop_nb_segs/output_2_all" u 1:2 w l lw 1 lc 6  t 'Erreur', fexp(log(x)) lc 0 dt 3 t sprintf("%.2fx+%.2f", a, b)

set output outputdir."nb_segs_diff_L2_cste.tex"
plot inputdir."loop_nb_segs/output_1_all" u 1:2 w l lw 1 lc 6  t 'Erreur', gexp(log(x)) lc 0 dt 3 t sprintf("%.2fx+%.2f", c, d)

